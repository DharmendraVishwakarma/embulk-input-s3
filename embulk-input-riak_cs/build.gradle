apply plugin: "jacoco"

group = "${rootProject.group}"
version = "${rootProject.version}"
description = "Reads files stored on Riak CS"

repositories {
    mavenCentral()
    jcenter()
    maven {
        url "https://dl.bintray.com/embulk-input-s3/maven"
    }
}

dependencies {
    compileOnly "org.embulk:embulk-api:0.10.5"
    compileOnly "org.embulk:embulk-core:0.10.5"

    compile(project(":embulk-input-s3")) {
        // They conflict with embulk-core. They are once excluded here,
        // and added explicitly with versions exactly the same with embulk-core:0.10.5.
        exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
        exclude group: "com.fasterxml.jackson.datatype", module: "jackson-datatype-jdk8"
        exclude group: "javax.validation", module: "validation-api"
        exclude group: "joda-time", module: "joda-time"
    }

    // They are once excluded from transitive dependencies of other dependencies,
    // and added explicitly with versions exactly the same with embulk-core:0.10.5.
    compile "com.fasterxml.jackson.core:jackson-annotations:2.6.7"
    compile "com.fasterxml.jackson.core:jackson-core:2.6.7"
    compile "com.fasterxml.jackson.core:jackson-databind:2.6.7"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.6.7"
    compile "javax.validation:validation-api:1.1.0.Final"
    compile "joda-time:joda-time:2.9.2"

    testCompile "junit:junit:4.13"
    testCompile "org.mockito:mockito-core:1.10.19"
    testCompile "org.embulk:embulk-api:0.10.5"
    testCompile "org.embulk:embulk-core:0.10.5"
    testCompile "org.embulk:embulk-core:0.10.5:tests"
    testCompile "org.embulk:embulk-deps-buffer:0.10.5"
    testCompile "org.embulk:embulk-deps-config:0.10.5"
    testCompile "org.embulk:embulk-deps-timestamp:0.10.5"
    testCompile "org.embulk:embulk-standards:0.10.5"
    testCompile project(":embulk-input-s3").sourceSets.test.output
}

embulkPlugin {
    mainClass = "org.embulk.input.riak_cs.RiakCsFileInputPlugin"
    category = "input"
    type = "riak_cs"
    ignoreConflicts = [
        // They conflict with embulk-core:0.10.5, but embulk-core will remove them from its dependencies.
        // They are intentionally included in the plugin's dependencies to keep it working before and after the removal.
        // Warning messages against them are marked to ignore explicitly here.
        [ group: "com.fasterxml.jackson.core", module: "jackson-annotations" ],
        [ group: "com.fasterxml.jackson.core", module: "jackson-core" ],
        [ group: "com.fasterxml.jackson.core", module: "jackson-databind" ],
        [ group: "com.fasterxml.jackson.datatype", module: "jackson-datatype-jdk8" ],
        [ group: "javax.validation", module: "validation-api" ],
        [ group: "joda-time", module: "joda-time" ],
    ]
}

gem {
    authors = [ "Sadayuki Furuhashi" ]
    email = [ "frsyuki@gmail.com" ]
    summary = "Riak CS file input plugin for Embulk"
    homepage = "https://github.com/embulk/embulk-input-s3"
    licenses = [ "Apache-2.0" ]
}
